#CP1251

[Алгебра\квадратное уравнение]
/* нахождение корней уравнения a*x^2 + b*x + c = 0 */
a=1; b=3; c=2;
D=sqrt(b^2-4*a*c;
print"Корни:";
print (-b-D)/2/a;
print (-b+D)/2/a;

[Алгебра\кубическое уравнение]
/* нахождение корней уравнения a*x^3 + b*x^2 + c*x + d = 0 */
a=1;b=2;c=3;d=4; u=-b/3/a; v=c/a; t=u^3-(u*v+d/a)/2; s=t^2+(v/3-u^2)*(v/3-u^2)^2; if(s>=0, goto complex, 0); s=2*sqrt(u^2-v/3); t=acos(t*8/s^3)/3;
print cos(t)*s+u; 
print cos(t+deg120)*s+u;
return cos(t+deg240)*s+u;   
complex: v=3#(t+sqrt s); t=3#(t-sqrt s);
print v+t+u; if(s<>0 or v<>0, return u-(v+t)/2+(v-t)*sqrt-3/2, 0);

[Алгебра\простые числа]
/* нахождение заданного количества простых чисел, начиная с заданного места: n=сколько считать, начиная с номера p; p=порядковый номер первого (начиная с нуля) */
n=100; p=1;
print p=prime p; n--; if(n>0, goto2, 0);

[Алгебра\делители]
/* вычисление делителей числа n */
n=12345678; print"n =",n;
p=0;k=0;e=1;c=1;j=n;m=1; cykl: print d=divisor j; e=e*(d-(p!=d));if(p==d,gotor4,0); c=c*m;m=1;k++; m++;p=d; j=j/d; if(j>=d, goto cykl, 0);
print;print"Число всех делителей:",c*m;print"Число простых делителей:",k;print"Функция Эйлера:",e

[Алгебра\факториал]
/* подсчёт факториала числа n */
n=1e9; print"n =",n;
print"Факториал n:",(n/exp1)^n*sqrt(2*pi*n)*(1+1/12/n+1/288/n^2-139/51840/n^3-571/2488320/n^4)


[Геометрия\прямоугольный параллелипипед]
/* расчёт параметров прямоугольного параллелипипеда */
a=1;b=2;c=3;
print"Объём:",a*b*c;
print"Площадь поверхности:",2*(a*b+b*c+a*c; print"Длина диагонали:",sqrt(a^2+b^2+c^2

[Геометрия\сфера]
/* расчёт параметров сферы */
r=5;  print"Объём:",4/3*pi*r^3; print"Площадь поверхности:",4*pi*r^2

[Геометрия\усеченный конус]
/* расчёт параметров усеченного конуса */
r1=1;r2=0;v=2;  print"Объём:",pi*v/3*(r1^2+r1*r2+r2^2; print"Площадь боковой поверхности:",pi*v*(r1+r2);
print"Полная площадь поверхности:",pi*(r1^2+r2^2+(r1+r2)*hypot(v,(r1-r2)/2


[Тригонометрия\a(gamma)b]
/* известны две стороны и угол лежащий между ними */
a=3;b=4;gamma=deg90;   print"Сторона c:",c=sqrt(a^2+b^2-2*a*b*cos gamma; print"Периметр:",o=a+b+c; print"Площадь:",S=a*b*sin gamma/2; print"Радиус вписанной окружности:",2*S/o; print "Радиус описанной окружности:",c/2/sin gamma;print; print"Высота, проведённая к стороне a:",2*S/a; print"Высота, проведённая к стороне b:",2*S/b; print"Высота, проведённая к стороне c:",2*S/c; print"Meдиана, проведённая к стороне a:",sqrt(2*(b^2+c^2)-a^2)/2; print"Meдиана, проведённая к стороне b:",sqrt(2*(a^2+c^2)-b^2)/2; print"Meдиана, проведённая к стороне c:",sqrt(2*(b^2+a^2)-c^2)/2; print; print"Угол alfa:",acos((b^2+c^2-a^2)/(2*b*c; print"Угол beta:",acos((a^2+c^2-b^2)/(2*a*c; 

[Тригонометрия\(beta)a(gamma)]
/* известны сторона и два прилежащих угла */
a=3;beta=deg40;gamma=deg50;   p=a/sin(beta+gamma; print"Сторона b:",b=p*sin beta; print"Сторона c:",c=p*sin gamma; print"Периметр:",o=a+b+c; v=a/(cot beta+cot gamma; print"Площадь:",S=a*v/2; print"Радиус вписанной окружности:",2*S/o; print "Радиус описанной окружности:",p/2;print; print"Высота, проведённая к стороне a:",v; print"Высота, проведённая к стороне b:",2*S/b; print"Высота, проведённая к стороне c:",2*S/c; print"Meдиана, проведённая к стороне a:",sqrt(2*(b^2+c^2)-a^2)/2; print"Meдиана, проведённая к стороне b:",sqrt(2*(a^2+c^2)-b^2)/2; print"Meдиана, проведённая к стороне c:",sqrt(2*(b^2+a^2)-c^2)/2; print; print"Угол alfa:",deg180-beta-gamma

[Тригонометрия\abc]
/* известны три стороны */
a=3;b=4;c=5;  print"Периметр:",a+b+c; p=ans/2;S=sqrt(p*(p-a)*(p-b)*(p-c; if(real ans,gotor3,0);print"В невырожденном треугольнике сумма длин двух сторон должна быть больше длины третьей стороны !";return;
print"Площадь:",S; print"Радиус вписанной окружности:",S/p; print "Радиус описанной окружности:",a*b*c/4/S;print; print"Высота, проведённая к стороне a:",2*S/a; print"Высота, проведённая к стороне b:",2*S/b; print"Высота, проведённая к стороне c:",2*S/c; print"Meдиана, проведённая к стороне a:",sqrt(2*(b^2+c^2)-a^2)/2; print"Meдиана, проведённая к стороне b:",sqrt(2*(a^2+c^2)-b^2)/2; print"Meдиана, проведённая к стороне c:",sqrt(2*(b^2+a^2)-c^2)/2; print; print"Угол alfa:",acos((b^2+c^2-a^2)/(2*b*c; print"Угол beta:",acos((a^2+c^2-b^2)/(2*a*c; print"Угол gamma:",acos((b^2+a^2-c^2)/(2*b*a;

[Тригонометрия\ab(alfa)]
/* известны две стороны и угол */
alfa=deg90;a=5;b=4;   print"Сторона c:",c=b*cos alfa+sqrt(a^2-(b*sin alfa)^2;  print"Периметр:",o=a+b+c; print"Площадь:",S=b*c*sin alfa/2; print"Радиус вписанной окружности:",2*S/o; print "Радиус описанной окружности:",a/2/sin alfa;print; print"Высота, проведённая к стороне a:",2*S/a; print"Высота, проведённая к стороне b:",2*S/b; print"Высота, проведённая к стороне c:",b*sin alfa; print"Медиана, проведённая к стороне a:",sqrt(2*(b^2+c^2)-a^2)/2; print"Meдиана, проведённая к стороне b:",sqrt(2*(a^2+c^2)-b^2)/2; print"Meдиана, проведённая к стороне c:",sqrt(2*(b^2+a^2)-c^2)/2; print; print"Угол beta:",acos((a^2+c^2-b^2)/(2*a*c; print"Угол gamma:",acos((b^2+a^2-c^2)/(2*b*a


[Статистика\Стандартное отклонение]
/* подсчёт дисперсии и стандартного отклонения */
x=(25, 24, 23, 22, 20, 18, 17, 16, 14, 10, 8);
a=ave x;
print"x-ave=", listfor(i,0,count x-1,x[i]-a), "
S(x-ave)^2=", sumfor(i,0,count x-1,(x[i]-a)^2), "
Дисперсия =", var x, "
Стандартное отклонение =", stdev x

[Статистика\Стандартное отклонение (с частотным списком)]
/* подсчёт дисперсии и стандартного отклонения */
Data=(14,13,12,11,10,9,8,7,6,5);
Freq=(1,2,3,3,5,3,2,1,2,1);
print "n=",n=sum Freq, "
sum x=", s=sum(Data*Freq), "
sum x^2=", s2=sumfor(i,0,count Data-1,Data[i]^2 * Freq[i]), "
mean=",s/n, "
Дисперсия =", v=(s2-s^2/n)/(n-1), "
Стандартное отклонение =", sqrt v

[Статистика\Корреляция Пирсона r]
/* измерение степени линейных связей между переменными (коэффициент корреляции Пирсона r) */
x=(100,40,95,90,92,85,55,60,98,20);
y=(0,95,5,20,30,40,50,70,0,100);
/* (n*(x*y)-sum(x)*sum(y))/sqrt((n*sumq(x)-sum(x)^2)*(n*sumq(y)-sum(y)^2) */
print "Коэффициент корреляции Пирсона r =", lrr(x',y')

[Статистика\Критерий Уилкоксона-Манна-Уитни]
/* оценка различий между двумя выборками по признаку, измеренному в количественной или порядковой шкале (U-критерий Манна-Уитни) */
L1=(12, 12, 11, 8, 7, 6, 6);L2=(24, 24, 16, 15, 14, 12, 11, 10, 9);
S1=sort(L1);S2=sort(L2); n1=count L1;n2=count L2; 
R1=0;R2=0;i=0;j=0;o=1;t1=0;t2=0;last=0;ties=0;
merge:
if(i<n1, goto merge1, 0);
if(j<n2, goto take2, goto addrank);
merge1: if(j>=n2, goto take1, 0);
if(S1[i]<S2[j], goto take1, 0);
take2: b=0; item=S2[j]; j++; goto compare;
take1: b=1; item=S1[i]; i++;
compare: if(item==last, goto next, 0);
addrank:
a=t1+t2; if(a>0,ties=ties+a-1,0);
a=o-(a+1)/2; R1=R1+a*t1; R2=R2+a*t2;
if(o>n1+n2, goto end, 0);
t1=0;t2=0; last=item;
next: o++; if(b,t1++,t2++); goto merge;
end:
U1=R1-n1*(n1+1)/2; U2=R2-n2*(n2+1)/2;
if(U1+U2==n1*n2, gotor3,0); print "error"; return;
print "SUM OF RANKS 1=",R1, "
SUM OF RANKS 2=",R2, "
NUMBER OF TIES=",ties, "
U=",U=max(U1,U2), "
MEAN U=",Mu=n1*n2/2, "
STD DEV U=",Su=sqrt(n1*n2*(n1+n2+1)/12), "
Z=",(U-Mu)/Su


[Программирование\Печать]
/* отображение текста и результатов вычислений */
print "add", 2+3, "mult", 2*3

/* ... с распечаткой на 2 строках */
print "add", 2+3; print "mult", 2*3

/* ... с распечаткой на 1 строке */
print "add", 2+3,; print " mult", 2*3

/* ... с вставкой пустой строки */
print "add", 2+3; print; print "mult", 2*3

/* автораспечатка последнего выражения */
a=2; b=3; c=a*b

/* отказ от печати последнего выражения, если в конце стоит запятая */
a=2; b=3; print c=a*b; d=a+b;